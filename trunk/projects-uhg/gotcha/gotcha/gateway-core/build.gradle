/**

GRADLE build for the Gotcha Gateway Server core jar

Does not build:

- wars
- specific ContentSources

*/

usePlugin 'java'

sourceCompatibility = 1.5
basefolder = "gateway-core"
archivesBaseName = "GOTCHA-$basefolder"
version = '1.0'
manifest.mainAttributes(
    'Implementation-Title': archivesBaseName,
    'Implementation-Version': version
)

// use local simple lib directory as repo...
repositories {
    flatDir(dirs: file("../artifacts/lib/$basefolder"))
}

// using artifact-only notation due to descriptor-less local flatdir "repository"
dependencies {
    compile (
        "java:activation@jar",
        "apache:commons-io-1.4@jar",
        "apache:commons-lang@jar",
        "apache:log4j-1.2.15@jar",
        "java:servlet-api@jar"
    )
    //testCompile group:'junit',name:'junit',version:'4.+'
}

// output statuses
compileJava.doLast { println "compile didWork = ${compileJava.didWork}" }
compileTestJava.doLast { println "compileTest didWork = ${compileTestJava.didWork}" }


// project info task

task projectinfo << {
    allprojects {
        println "project path $path"
        println "  project name = $name"
        println "  project dir = '${rootProject.relativePath(projectDir)}'"
        println "  build file = '${rootProject.relativePath(buildFile)}'"
        println "  build dir = '${rootProject.relativePath(buildDir)}'"
    }
}

// "upload" finished product to the artifacts "repo"

uploadArchives {
    repositories {
       flatDir(dirs: file("../artifacts/built/$basefolder-$version/"))
    }
}